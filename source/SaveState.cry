import UserData;

const CONFIG_FILE = 'sentientsewage_data.txt';

class SaveState {
	static field levels_unlocked = null;
	
	constructor() { }
	
	static function is_level_unlocked(difficulty) {
		if (difficulty == 'easy') return true;
		
		if (SaveState.levels_unlocked == null) {
			SaveState.levels_unlocked = {};
			if (UserData.fileExists(CONFIG_FILE)) {
				data = UserData.fileReadText(CONFIG_FILE).trim().lower();
				for (item : data.split('|')) {
					SaveState.levels_unlocked[item.trim().lower()] = true;
				}
			}
		}
		return SaveState.levels_unlocked.get(difficulty, false) == true;
	}
	
	static function unlock(difficulty) {
		if (!SaveState.is_level_unlocked(difficulty)) {
			SaveState.levels_unlocked[difficulty] = true;
			SaveState._save_data();
		}
	}
	
	static function _save_data() {
		output = [];
		for (diff : ['medium', 'hard', 'insane']) {
			if (SaveState.is_level_unlocked(diff)) {
				output.add(diff);
			}
		}
		if (output.length > 0) {
			UserData.fileWriteText(CONFIG_FILE, output.join('|'));
		}
	}
}
