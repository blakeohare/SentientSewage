import Graphics;
import ImageResources;
import Math;

class ImageLibrary {
	static field images = {};
	static field images_by_scale = {};
	static field reversed = {};
	static field sheet = null;
	
	constructor() { }
	
	static function get(path) {
		img = ImageLibrary.images.get(path, null);
		if (img == null) {
			img = ImageLibrary.get_by_scale(path, 1);
			ImageLibrary.images[path] = img;
		}
		return img;
	}
	
	static function get_reverse(path) {
		img = ImageLibrary.reversed.get(path, null);
		if (img == null) {
			img = ImageLibrary.get(path);
			img = img.flip(true, false);
			ImageLibrary.reversed[path] = img;
		}
		return img;
	}
	
	static function get_by_scale(path, ratio) {
		scale = Math.floor(ratio * 1000);
		lookup = ImageLibrary.images_by_scale.get(scale, null);
		if (lookup == null) {
			lookup = {};
			ImageLibrary.images_by_scale[scale] = lookup;
		}
		
		img = lookup.get(path, null);
		if (img == null) {
			if (scale == 1000) {
				rpath = 'images/' + path;
				img = GraphicsTexture.createImage(ImageLibrary.sheet.getImage(rpath));
				lookup[path] = img;
			} else {
				img = ImageLibrary.get_by_scale(path, 1);
				width = Math.floor(img.width * ratio);
				height = Math.floor(img.height * ratio);
				img = img.scale(width, height);
				lookup[path] = img;
			}
		}
		
		return img;
	}
}
