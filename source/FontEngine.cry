import Resources;

class FontEngine {
	static field images = null;
	
	constructor() { }
	
	static function render(text, x, y, size) {
		if (FontEngine.images == null) {
			FontEngine.initialize();
		}
		lookup = FontEngine.images[size];
		img = null;
		space = lookup['o'].width;
		
		for (char : text) {
			if (char == ' ') {
				x += space;
			} else {
				img = lookup.get(char, null);
				if (img == null) {
					img = lookup['?'];
				}
				
				img.draw(x, y);
				x += img.width + 1;
			}
		}
		
		height = img == null ? 0 : img.height;
		return [x, y + height];
	}
	
	static function initialize() {
		data = Resources.readText('images/font/lookup.txt')
			.trim()
			.replace('\r\n', '\n')
			.replace('\r', '\n')
			.split('\n');
		raw_file_info = {};
		
		FontEngine.images = {};
		sizes = {
			'S': 1,
			'M': 2,
			'L': 3,
			'XL': 6,
		};
		for (tshirt : sizes.keys()) {
			scale = sizes[tshirt];
			lookup = {};
			for (row : data) {
				char = row[0];
				file = 'font/' + row[1:] + '.png';
				img = ImageLibrary.get_by_scale(file, scale);
				lookup[char] = img;
			}
			
			FontEngine.images[tshirt] = lookup;
			FontEngine.images[tshirt.lower()] = lookup;
		}
	}
}
