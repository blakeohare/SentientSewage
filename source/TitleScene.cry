import Game;
import Graphics2D;
import Math;

class TitleScene : AbstractScene {
	field bounds;
	field active_hover;
	field my_rc = 0;
	
	constructor() : base() { }
	
	function hit_test(x, y) {
		if (this.bounds == null) return null;
		for (item : this.bounds.keys()) {
			bounds = this.bounds[item];
			if (x > bounds[0] && x < bounds[2] && y > bounds[1] && y < bounds[3]) {
				return item;
			}
		}
		return null;
	}
	
	function update(events) {
		for (event : events) {
			switch (event.type) {
				case EventType.MOUSE_MOVE:
					old = this.active_hover;
					this.active_hover = this.hit_test(event.x, event.y);
					if (this.active_hover != old) {
						this.my_rc = 0;
					}
					break;
				case EventType.MOUSE_LEFT_DOWN:
					old = this.active_hover;
					this.active_hover = this.hit_test(event.x, event.y);
					if (this.active_hover != old) {
						this.my_rc = 0;
					}
					if (this.active_hover != null) {
						switch (this.active_hover) {
							case 'intro':
								this.switch_scene(new FadeTransitionScene(this, new IntroScene()));
								break;
							case 'about':
								this.switch_scene(new FadeTransitionScene(this, new AboutScene()));
								break;
							case 'easy':
							case 'medium':
							case 'hard':
							case 'insane':
								if (SaveState.is_level_unlocked(this.active_hover)) {
									this.switch_scene(new FadeTransitionScene(this, new PlayScene(this.active_hover)));
								}
								break;
							default:
								break;
						}
					}
					break;
			}
		}
	}
	
	function get_bounce(item) {
		if (item == this.active_hover && this.bounds != null)
			return -Math.floor(8 * Math.abs(Math.sin(this.my_rc * TWO_PI / FPS)));
		return 0;
	}
	
	function render(rc) {
	
		record_bounds = this.bounds == null;
		bounds = {};
		
		FontEngine.render('SENTIENT SEWAGE', 100, 20, 'XL');
		
		images = [
			'easy',
			'medium',
			'hard',
			'insane'
		];
		
		titles = [
			'Easy',
			'Medium',
			'Hard',
			'Insane',
		];
		
		lock = ImageLibrary.get('locked.png');
		
		y = GAME_HEIGHT / 2;
		for (i = 1; i < 5; ++i) {
			x = GAME_WIDTH * i / 5;
			difficulty = images[i - 1];
			img = ImageLibrary.get(difficulty + '.png');
			if (i == 1) {
				x -= 30;
			}
			img.draw(x - img.width / 2, y - img.height / 2);
			
			if (record_bounds) {
				bounds[difficulty] = [x - 90, y - 100, x + 90, y + 100];
			}
			
			available = SaveState.is_level_unlocked(difficulty);
			
			if (!available) {
				Draw.rectangle(x - 90, y - 100, 180, 200, 180, 180, 180, 200);
				lock.draw(x - lock.width / 2, y - lock.height / 2);
			}
			
			FontEngine.render(titles[i - 1], x - 50, y + 60 + this.get_bounce(difficulty), 'M');
		}
		
		base_y = GAME_HEIGHT * 4 / 5;
		y = base_y + this.get_bounce('intro');
		x = GAME_WIDTH / 4 - 40;
		xy = FontEngine.render('Introduction', x, y, 'L');
		if (record_bounds) bounds['intro'] = [x, y, xy[0], xy[1]];
		
		x = 3 * GAME_WIDTH / 4 - 40;
		y = base_y + this.get_bounce('about');
		xy = FontEngine.render('About', x, y, 'L');
		if (record_bounds) bounds['about'] = [x, y, xy[0], xy[1]];
		
		
		if (record_bounds) {
			this.bounds = bounds;
		}
		this.my_rc++;
	}
}